function(glob_source OUT_VARIABLE CXX_SOURCE_DIRECTORY)
    set(SOURCE_EXTENSIONS .c .i .cc .cp .ii .cu .c++ .cpp .cxx .ino .cppm)
    file(GLOB_RECURSE ALL_FILES "${CXX_SOURCE_DIRECTORY}/**")
    set(${OUT_VARIABLE} "")
    foreach(FILE_PATH IN LISTS ALL_FILES)
        if(NOT IS_DIRECTORY "${FILE_PATH}")
            get_filename_component(EXTENSION "${FILE_PATH}" EXT)
            string(TOLOWER "${EXTENSION}" EXTENSION)
            if (EXTENSION IN_LIST SOURCE_EXTENSIONS)
                list(APPEND ${OUT_VARIABLE} "${FILE_PATH}")
            endif()
        endif()
    endforeach()
    set(${OUT_VARIABLE} "${${OUT_VARIABLE}}" PARENT_SCOPE)
endfunction()

function(glob_header OUT_VARIABLE CXX_HEADER_DIRECTORY)
    set(HEADER_EXTENSIONS .h .hh .h++ .hpp .hxx .ixx .inl .cuh .tcc)
    file(GLOB_RECURSE ALL_FILES "${CXX_HEADER_DIRECTORY}/**")
    set(${OUT_VARIABLE} "")
    foreach(FILE_PATH IN LISTS ALL_FILES)
        if(NOT IS_DIRECTORY "${FILE_PATH}")
            get_filename_component(EXTENSION "${FILE_PATH}" EXT)
            string(TOLOWER "${EXTENSION}" EXTENSION)
            if (EXTENSION IN_LIST HEADER_EXTENSIONS)
                list(APPEND ${OUT_VARIABLE} "${FILE_PATH}")
            endif()
        endif()
    endforeach()
    set(${OUT_VARIABLE} "${${OUT_VARIABLE}}" PARENT_SCOPE)
endfunction()

file(GLOB SOURCE_FOLDERS LIST_DIRECTORIES true "${CMAKE_SOURCE_DIR}/*")
list(FILTER SOURCE_FOLDERS EXCLUDE REGEX "/\\.[^/]+/?$")
list(FILTER SOURCE_FOLDERS EXCLUDE REGEX "/cmake-build[^/]+/?$")

foreach (SOURCE_FOLDER IN LISTS SOURCE_FOLDERS)
    if (IS_DIRECTORY ${SOURCE_FOLDER})
        file(GLOB_RECURSE GLOB_PATHS "${SOURCE_FOLDER}/*.cmake")

        foreach (GLOB_PATH ${GLOB_PATHS})
            get_filename_component(ABS_GLOB_PATH "${GLOB_PATH}" ABSOLUTE)
            get_filename_component(ABS_GLOB_NAME "${ABS_GLOB_PATH}" NAME)
            get_filename_component(ABS_GLOB_DIR "${ABS_GLOB_PATH}" DIRECTORY)
            get_filename_component(TARGET_NAME "${ABS_GLOB_DIR}" NAME)

            list(FIND STRUCTURED_DIRECTORIES_SKIP "${TARGET_NAME}" POS)
            if (NOT POS EQUAL -1)
                continue()
            endif ()

            if (ABS_GLOB_NAME STREQUAL "shared.cmake")
                list(APPEND SHARED_DIRS "${ABS_GLOB_DIR}")
            elseif (ABS_GLOB_NAME STREQUAL "static.cmake")
                list(APPEND STATIC_DIRS "${ABS_GLOB_DIR}")
            elseif (ABS_GLOB_NAME STREQUAL "header.cmake")
                list(APPEND HEADER_DIRS "${ABS_GLOB_DIR}")
            elseif (ABS_GLOB_NAME STREQUAL "executable.cmake")
                list(APPEND EXEC_DIRS "${ABS_GLOB_DIR}")
            elseif (BUILD_TESTING AND ABS_GLOB_NAME STREQUAL "tests.cmake")
                list(APPEND TEST_DIRS "${ABS_GLOB_DIR}")
            elseif (ABS_GLOB_NAME STREQUAL "extern.cmake")
                list(APPEND EXTERN_DIRS "${ABS_GLOB_DIR}")
            endif ()
        endforeach ()
    endif ()
endforeach ()
