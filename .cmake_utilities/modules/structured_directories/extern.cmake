macro(push_cache_var VAR NEW_VALUE VAR_TYPE)
    get_property(_orig_${VAR}_entry CACHE ${VAR} PROPERTY TYPE)
    if (_orig_${VAR}_entry)
        set(_orig_${VAR}_defined ON)
        get_property(_orig_${VAR}_value CACHE ${VAR} PROPERTY VALUE)
        get_property(_orig_${VAR}_doc CACHE ${VAR} PROPERTY HELPSTRING)
        set(${VAR} ${NEW_VALUE} CACHE ${_orig_${VAR}_entry} "Temporarily setting ${VAR} for extern" FORCE)
    else ()
        set(_orig_${VAR}_defined OFF)
        set(${VAR} ${NEW_VALUE} CACHE ${VAR_TYPE} "Temporarily setting ${VAR} for extern" FORCE)
    endif ()
endmacro()

macro(pop_cache_var VAR)
    if (_orig_${VAR}_defined)
        set(${VAR} ${_orig_${VAR}_value} CACHE ${_orig_${VAR}_entry} "${_orig_${VAR}_doc}" FORCE)
    else ()
        unset(${VAR} CACHE)
    endif ()
endmacro()

foreach (TARGET_DIR ${EXTERN_DIRS})
    get_filename_component(TARGET_NAME "${TARGET_DIR}" NAME)

    push_cache_var(BUILD_TESTING OFF BOOL)
    push_cache_var(CMAKE_WARN_DEPRECATED OFF BOOL)
    push_cache_var(CMAKE_MESSAGE_LOG_LEVEL ERROR INTERNAL)

    include("${TARGET_DIR}/extern.cmake")

    pop_cache_var(CMAKE_MESSAGE_LOG_LEVEL)
    pop_cache_var(CMAKE_WARN_DEPRECATED)
    pop_cache_var(BUILD_TESTING)

    message("${PROJECT_NAME} - Added External Dependency  ${TARGET_NAME}")
endforeach ()
