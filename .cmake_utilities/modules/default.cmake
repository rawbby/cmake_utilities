include_guard(GLOBAL)

get_filename_component(MODULE_NAME "${CMAKE_CURRENT_LIST_FILE}" NAME_WE)
message("${PROJECT_NAME} - Added Module               ${MODULE_NAME}")

# Standard CMake output directory variables
set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/lib")
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/lib")
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/bin")

# Custom install directory variables (not in the CMake standard)
set(CMAKE_ARCHIVE_INSTALL_DIRECTORY "${CMAKE_INSTALL_PREFIX}/lib")
set(CMAKE_LIBRARY_INSTALL_DIRECTORY "${CMAKE_INSTALL_PREFIX}/lib")
set(CMAKE_RUNTIME_INSTALL_DIRECTORY "${CMAKE_INSTALL_PREFIX}/bin")
set(CMAKE_INCLUDE_INSTALL_DIRECTORY "${CMAKE_INSTALL_PREFIX}/include")

# Also handle multi config generators
if (CMAKE_CONFIGURATION_TYPES)
    list(LENGTH CMAKE_CONFIGURATION_TYPES CMAKE_CONFIGURATION_TYPES_LENGTH)
    if (CMAKE_CONFIGURATION_TYPES_LENGTH EQUAL 1)
        string(TOUPPER "${CMAKE_BUILD_TYPE}" CMAKE_BUILD_TYPE_UPPER)

        # Standard CMake output directory variables
        set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY_${CMAKE_BUILD_TYPE_UPPER} "${CMAKE_BINARY_DIR}/lib")
        set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_${CMAKE_BUILD_TYPE_UPPER} "${CMAKE_BINARY_DIR}/lib")
        set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_${CMAKE_BUILD_TYPE_UPPER} "${CMAKE_BINARY_DIR}/bin")

        # Custom install directory variables (not in the CMake standard)
        set(CMAKE_ARCHIVE_INSTALL_DIRECTORY_${CMAKE_BUILD_TYPE_UPPER} "${CMAKE_INSTALL_PREFIX}/lib")
        set(CMAKE_LIBRARY_INSTALL_DIRECTORY_${CMAKE_BUILD_TYPE_UPPER} "${CMAKE_INSTALL_PREFIX}/lib")
        set(CMAKE_RUNTIME_INSTALL_DIRECTORY_${CMAKE_BUILD_TYPE_UPPER} "${CMAKE_INSTALL_PREFIX}/bin")
        set(CMAKE_INCLUDE_INSTALL_DIRECTORY_${CMAKE_BUILD_TYPE_UPPER} "${CMAKE_INSTALL_PREFIX}/include")
    else ()
        foreach (CMAKE_CONFIGURATION_TYPE ${CMAKE_CONFIGURATION_TYPES})
            string(TOLOWER "${CMAKE_CONFIGURATION_TYPE}" CMAKE_CONFIGURATION_TYPE_LOWER)
            string(TOUPPER "${CMAKE_CONFIGURATION_TYPE}" CMAKE_CONFIGURATION_TYPE_UPPER)

            # Standard CMake output directory variables
            set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY_${CMAKE_CONFIGURATION_TYPE_UPPER} "${CMAKE_BINARY_DIR}/${CMAKE_CONFIGURATION_TYPE_LOWER}/lib")
            set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_${CMAKE_CONFIGURATION_TYPE_UPPER} "${CMAKE_BINARY_DIR}/${CMAKE_CONFIGURATION_TYPE_LOWER}/lib")
            set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_${CMAKE_CONFIGURATION_TYPE_UPPER} "${CMAKE_BINARY_DIR}/${CMAKE_CONFIGURATION_TYPE_LOWER}/bin")

            # Custom install directory variables (not in the CMake standard)
            set(CMAKE_ARCHIVE_INSTALL_DIRECTORY_${CMAKE_CONFIGURATION_TYPE_UPPER} "${CMAKE_INSTALL_PREFIX}/${CMAKE_CONFIGURATION_TYPE_LOWER}/lib")
            set(CMAKE_LIBRARY_INSTALL_DIRECTORY_${CMAKE_CONFIGURATION_TYPE_UPPER} "${CMAKE_INSTALL_PREFIX}/${CMAKE_CONFIGURATION_TYPE_LOWER}/lib")
            set(CMAKE_RUNTIME_INSTALL_DIRECTORY_${CMAKE_CONFIGURATION_TYPE_UPPER} "${CMAKE_INSTALL_PREFIX}/${CMAKE_CONFIGURATION_TYPE_LOWER}/bin")
            set(CMAKE_INCLUDE_INSTALL_DIRECTORY_${CMAKE_CONFIGURATION_TYPE_UPPER} "${CMAKE_INSTALL_PREFIX}/${CMAKE_CONFIGURATION_TYPE_LOWER}/include")
        endforeach ()
    endif ()
endif ()

if (NOT DEFINED CMAKE_CXX_STANDARD)
    foreach (MAX_CXX_STANDARD 23 20 17 14 11)
        try_compile(COMPILE_RESULT
                "${CMAKE_BINARY_DIR}/cxx_standard_${MAX_CXX_STANDARD}_test"
                SOURCES "${CMAKE_SOURCE_DIR}/.cmake_utilities/scripts/cxx_standard_${MAX_CXX_STANDARD}_test.cpp"
                CMAKE_FLAGS "-DCMAKE_CXX_STANDARD=${MAX_CXX_STANDARD}")
        if(COMPILE_RESULT)
            set(CMAKE_CXX_STANDARD ${MAX_CXX_STANDARD})
            message("set CMAKE_CXX_STANDARD                 ${CMAKE_CXX_STANDARD} (highest supported C++ standard)")
            break()
        endif()
    endforeach ()
endif ()

if (DEFINED CMAKE_CXX_STANDARD AND NOT DEFINED CMAKE_CXX_STANDARD_REQUIRED)
    set(CMAKE_CXX_STANDARD_REQUIRED ON)
    message("set CMAKE_CXX_STANDARD_REQUIRED        ${CMAKE_CXX_STANDARD_REQUIRED} (default)")
endif ()

if (NOT DEFINED CMAKE_CXX_EXTENSIONS)
    set(CMAKE_CXX_EXTENSIONS OFF)
    message("set CMAKE_CXX_EXTENSIONS               ${CMAKE_CXX_EXTENSIONS} (default)")
endif ()

if (NOT DEFINED CMAKE_INTERPROCEDURAL_OPTIMIZATION)
    set(CMAKE_INTERPROCEDURAL_OPTIMIZATION ON)
    message("set CMAKE_INTERPROCEDURAL_OPTIMIZATION ${CMAKE_INTERPROCEDURAL_OPTIMIZATION} (default)")
endif ()

# Enable or disable building of test targets
option(BUILD_TESTING "Build Tests" OFF)
